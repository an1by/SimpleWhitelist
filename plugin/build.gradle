plugins {
    id 'java'
}

version = project.project_version
group = project.group

configurations {
    shade
    compile.extendsFrom shade
}

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype-snapshots"
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    shade project(path: ':common')
    implementation project(path: ":common")
    compileOnly "org.spigotmc:spigot-api:1.21-R0.1-SNAPSHOT"
    compileOnly "net.md-5:bungeecord-api:1.21-R0.1-SNAPSHOT"
    compileOnly "com.velocitypowered:velocity-api:3.2.0-SNAPSHOT"
    annotationProcessor "com.velocitypowered:velocity-api:3.2.0-SNAPSHOT"
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    var replaceProperties = [
            mod_name         : project.mod_name,
            mod_id           : mod_id,
            mod_version      : project.project_version,
            mod_authors      : project.mod_authors,
            mod_description  : project.mod_description,
            contact_homepage : project.contact_homepage
    ]

    inputs.properties replaceProperties

    filteringCharset "UTF-8"
    filesMatching(["**/bungee.yml", "**/plugin.yml", "**/velocity-plugin.json"]) {
        expand replaceProperties + [project: project]
    }
}

jar {
    configurations.shade.each { dep ->
        duplicatesStrategy = DuplicatesStrategy.WARN
        from(project.zipTree(dep)) {
            exclude 'META-INF/MANIFEST.MF', 'module-info.class'
        }
    }
    archiveBaseName.set("SimpleWhitelist")
    archiveClassifier.set("")
    archiveAppendix.set("SpigotProxy")
}
