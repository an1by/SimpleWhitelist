plugins {
    id 'net.minecraftforge.gradle' version "${forgegradle_version}"
    id 'org.spongepowered.mixin' version "${mixingradle_version}"
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'com.modrinth.minotaur'
apply plugin: 'mod-update'

apply from: "https://raw.githubusercontent.com/henkelmax/mod-gradle-scripts/${mod_gradle_script_version}/mod.gradle"

processResources {
    filesMatching('**/*.toml') {
        expand 'mod_version': mod_version,
                'forge_dependency': forge_dependency,
                'minecraft_version': minecraft_version
    }
}

repositories {
    maven { url = 'https://maven.shedaniel.me/' }
}

dependencies {
    implementation 'org.jetbrains:annotations:23.0.0'

    implementation('net.sf.jopt-simple:jopt-simple:5.0.4') { version { strictly '5.0.4' } }
}

tasks.register('generateJava', Copy) {
    from project(':common').file('src/template/java')
    into "${layout.buildDirectory.asFile.get()}/generated/java"
    expand 'minecraft_version': minecraft_version
}
sourceSets.main.java {
    srcDir "${layout.buildDirectory.asFile.get()}/generated/java"
}
compileJava.dependsOn generateJava

shadowJar {
    relocate 'org.aopalliance', "${project.group}.shadow.org.aopalliance"
    relocate "javax.inject", "${project.group}.shadow.javax.inject"
    exclude "net.aniby.simplewhitelist.common.plugin"
    exclude "whitelist.txt"
    exclude "default_config.json"
    exclude 'natives/*-x86/*'
}
