buildscript {
    dependencies {
        classpath "info.u-team.curse_gradle_uploader:curse_gradle_uploader:${curse_gradle_uploader_version}"
    }
}

plugins {
    id 'java'
}

apply plugin: 'io.papermc.paperweight.userdev'
apply plugin: 'com.gradleup.shadow'
apply plugin: 'com.modrinth.minotaur'
apply plugin: 'info.u_team.curse_gradle_uploader'
apply plugin: 'io.papermc.hangar-publish-plugin'
apply plugin: 'xyz.jpenilla.run-paper'

apply from: "https://raw.githubusercontent.com/henkelmax/mod-gradle-scripts/${mod_gradle_script_version}/mod.gradle"

repositories {
    maven { url = 'https://repo.viaversion.com/' }
}

tasks {
    runServer {
        downloadPlugins {
            modrinth('viaversion', "5.4.1-SNAPSHOT+756")
            modrinth('viabackwards', "5.4.1-SNAPSHOT+438")
        }
    }
}

processResources {
    filesMatching('paper-plugin.yml') {
        expand 'mod_version': mod_version,
                'minecraft_version': minecraft_version
    }
}

tasks.register('generateJava', Copy) {
    from project(':common').file('src/template/java')
    into "${layout.buildDirectory.asFile.get()}/generated/java"
    expand 'minecraft_version': minecraft_version,
            'mod_version': mod_version
}
sourceSets.main.java {
    srcDir "${layout.buildDirectory.asFile.get()}/generated/java"
}
compileJava.dependsOn generateJava

shadowJar {
    exclude 'natives/*-x86/*'
}
