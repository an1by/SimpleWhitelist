plugins {
    id 'net.neoforged.gradle.userdev' version "${neogradle_version}"
    id 'net.neoforged.gradle.mixin' version "${neogradle_mixin_version}"
}

apply plugin: 'com.gradleup.shadow'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'com.modrinth.minotaur'
apply plugin: 'mod-update'

apply from: "https://raw.githubusercontent.com/henkelmax/mod-gradle-scripts/${mod_gradle_script_version}/mod.gradle"

processResources {
    filesMatching('**/*.toml') {
        expand 'mod_version': mod_version,
                'neoforge_dependency': neoforge_dependency,
                'minecraft_version': minecraft_version
    }
}

repositories {
    maven { url = 'https://maven.shedaniel.me/' }
}

dependencies {
    implementation jarJar("net.kyori:adventure-platform-neoforge:6.4.0")
    implementation jarJar("org.yaml:snakeyaml:${snakeyaml_version}")
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

tasks.register('generateJava', Copy) {
    from project(':common').file('src/template/java')
    into "${layout.buildDirectory.asFile.get()}/generated/java"
    expand 'minecraft_version': minecraft_version
}
sourceSets.main.java {
    srcDir "${layout.buildDirectory.asFile.get()}/generated/java"
}
compileJava.dependsOn generateJava

shadowJar {
    exclude 'natives/*-x86/*'
}
